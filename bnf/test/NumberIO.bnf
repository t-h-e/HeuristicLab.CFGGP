// *****************************************************************************
// NumberIO grammar
// *****************************************************************************

// structure

<predefined> ::= 'i0 = i1 = i2 = int()'
                 'f0 = f1 = f2 = float()'
                 'res = float()'
                 <code>

// *****************************************************************************
<int_var> ::= 'i0' | 'i1' | 'i2' | 'in0'
<float_var> ::= 'f0' | 'f1' | 'f2' | 'in1' | 'res'
// *****************************************************************************


<code> ::= <code><statement>'\n' | <statement>'\n'
<statement> ::= <assign>

            // from int
<assign> ::= <int_assign>
            // from float
           | <float_assign>

// int

<int_assign> ::= <int_var> '=' <int> | <int_var> <arith_assign> <int>

<int_var_const> ::= <int_var> | <int_const>

<int> ::= <int_var> | <int_const>
        // | <arith_prefix><int>
        | '(' <int_var_const> <arith_ops> <int_var_const> ')'
        | <int_arith_ops_protected>'('<int_var_const>','<int_var_const>')'
        | 'min('<int_var_const>', '<int_var_const>')'
        | 'max('<int_var_const>', '<int_var_const>')'
        // | 'abs('<int>')'
        // from float
        | 'int(math.ceil('<float>'))' | 'int(math.floor('<float>'))' | 'int(round('<float>'))' | 'int('<float>')'

// to avoid problems with leading zeros in python int
<int_const> ::= 'int('<number>'.0)'
<number> ::= <num> | <num><num> | <num><num><num> | <num><num><num><num> | <num><num><num><num><num>
           | <num><num><num><num><num><num> | <num><num><num><num><num><num><num>
           | <num><num><num><num><num><num><num><num> | <num><num><num><num><num><num><num><num><num>
           | <num><num><num><num><num><num><num><num><num><num>
<num> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

<arith_assign> ::= <arith_ops>'='
<arith_ops> ::= '+'|'-'|'*'
// ** raises ZeroDivision error when 'a = 0; a ** ~a'
// |'**'

<int_arith_ops_protected> ::= 'divInt' | 'mod'
<arith_prefix> ::= '+'|'-'
//|'~'

// float

<float_assign> ::= <float_var> '=' <float> | <float_var> <arith_assign> <float>

<float_var_const> ::= <float_var> | <float_const>

<float> ::= <float_var> | <float_const> | <int>
        // | <arith_prefix><float>
        | '(' <float_var_const> <arith_ops> <float_var_const> ')' | '(' <int_var_const> <arith_ops> <float_var_const> ')' | '(' <float_var_const> <arith_ops> <int_var_const> ')'
        | <float_arith_ops_protected>'('<float_var_const>','<float_var_const>')' | <float_arith_ops_protected>'('<int_var_const>','<float_var_const>')' | <float_arith_ops_protected>'('<float_var_const>','<int_var_const>')'
        | 'min('<float_var_const>', '<float_var_const>')' | 'min('<int_var_const>', '<float_var_const>')' | 'min('<float_var_const>', '<int_var_const>')'
        | 'max('<float_var_const>', '<float_var_const>')' | 'max('<int_var_const>', '<float_var_const>')' | 'max('<float_var_const>', '<int_var_const>')'

<float_const> ::= <number>'.'<number>

<float_arith_ops_protected> ::= 'div' | 'divInt' | 'mod'
