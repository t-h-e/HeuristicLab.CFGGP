// *****************************************************************************
// list grammar for string list
// *****************************************************************************
//
// depends on: string (string, string_var)
// return: string
//         bool
//         int
// *****************************************************************************

<list_string_var> ::= 'list_string'

<list_string_assign> ::= <list_string_var>' = '<list_string>

<list_string> ::= <list_string_var> | <list_string_slice>
                | 'saveSplit('<string>','<string>')'
<list_string_slice> ::= <list_string_var>'['<int>':'<int>']' | <list_string_var>'[:'<int>']' | <list_string_var>'['<int>':]'

<getListIndexCall_string> ::= 'getIndexStringList('<list_string>', '<int>')'
<setListIndexToCall_string> ::= 'setListIndexTo('<list_string>', '<int>', '<string>')'
<deleteListItemCall_string> ::= 'deleteListItem('<list_string>', '<int>')'

// Add to

<call> ::= <list_string_var>'.append('<string>')'
         | <list_string_var>'.insert('<int>','<string>')'
         | <deleteListItemCall_string>
         | <setListIndexToCall_string>

<for> ::= <loop-header>'for forCounter% in '<list_string><loop-block>
        | <loop-header>'for '<string_var>' in '<list_string><loop-block>

// Generic for all

<in_list_comp_op> ::= 'in'|'not in'

// Return int

<ret_int> ::= 'len('<list_string>')'

// Return string

<ret_string> ::= <getListIndexCall_string>
             | <string>'.join('<list_string>')'

// Return bool

<ret_bool> ::= <string> <in_list_comp_op> <list_string>

// *****************************************************************************
// Helper Code
// *****************************************************************************

def saveSplit(curString, separator):
  if not separator:
    return []
  return curString.split(separator)


def getIndexStringList(curList, index):
  if not curList:
    return str()
  return curList[index % len(curList)]
